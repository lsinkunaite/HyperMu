#######################################################
##                                                   ##
##                 admagnetic.g4bl                   ##
##                                                   ##
##  Simulation of a beam source of:                  ##
##    (1) muon decay                                 ##
##    (2) gold cascade                               ##
##  positioned in the 5T magnetic field. Simplified  ##
##  geometry.                                        ##
##                                                   ##
#######################################################

include definitions.g4bl    #definitions

#number of simulated events
param evts=1e5

physics QGSP_BERT_EMX disable=muMinusCaptureAtRest

#Define some parameters
param name=mudecay
#param name=goldcascade
#param det=NaI
param det=BGO


param P=0
param size=200
#param steppingVerbose=1
param worldMaterial=Vacuum maxStep=100.0
param histoFile=../../simdata/magnetic/adBfield_5T_SDFInx2_2mm_Al_det_$det$evts$name.root

trackcuts keep=e-,e+,gamma,mu-,mu+


# Define source
# (1) muon decay
beam gaussian beamX=0 beamY=0 beamZ=0 meanP=$P nEvents=$evts particle=mu-

# (2) gold cascade
#beam goldcascade beamX=0 beamY=0 beamZ=0 meanP=$P nEvents=$evts particle=gamma

# Geometry
box SciBoxFront width=200.0 height=200.0 length=4.0 material=Scintillator color=$SciColor
box SciBoxFrontIn width=200.0 height=200.0 length=1.0 material=Scintillator color=$SciColor
box SciBoxEnd width=100.0 height=100.0 length=10.0 material=Scintillator color=$SciColor
box H2Box width=200.0 height=200.0 length=50.0 material=H2 color=$H2Color
box AlBox width=200.0 height=200.0 length=2.0 material=Al color=$AlColor
tube NaItube innerRadius=60.0 outerRadius=100.0 initialPhi=0 finalPhi=360 length=300.0 material=SODIUM_IODIDE color=$BGOColor
tube BGOtube innerRadius=65.0 outerRadius=100.0 initialPhi=0 finalPhi=360 length=290.0 material=BGO color=$BGOColor

# -- BGO protection with Al
tube AlDisk innerRadius=60.0 outerRadius=100.0 initialPhi=0 finalPhi=360 length=5.0 material=Al color=$AlColor
tube Altube innerRadius=60.0 outerRadius=65.0 initialPhi=0 finalPhi=360 length=290.0 material=Al color=$AlColor

# Detectors
detector TubeDet solid=BGOtube format=rootExtended material=BGO color=$BGOColor
detector SciDetFront solid=SciBoxFront format=rootExtended material=Scintillator color=$SciColor
detector SciDetEnd solid=SciBoxEnd format=rootExtended material=Scintillator color=$SciColor
detector SciDetFrontIn1 solid=SciBoxFrontIn format=rootExtended material=Scintillator color=$SciColor
detector SciDetFrontIn2 solid=SciBoxFrontIn format=rootExtended material=Scintillator color=$SciColor


# Cylindrical B-field
fieldexpr Bfield radius=500 length=1000 Bz=5.0
place Bfield z=0


place SciDetFrontIn1 z=12.25 parent=H2Box
place SciDetFrontIn2 z=12.75 parent=H2Box
place H2Box z=0
place AlBox z=27
#place SciDetFront z=31
place AlDisk z=35.5
place Altube z=183
place TubeDet z=183
place AlDisk z=330.5
place SciDetEnd z=388




