##########################################################
##                                                      ##
##                    thesiscav.g4bl                    ##
##                                                      ##
##  Simulation of a beam source of:                     ##
##    (1) muon decay                                    ##
##    (2) gold cascade                                  ##
##  positioned at the center of two rectangular-shaped  ##
##  5-mm plastic scintillator detectors plus a 250-mm/  ##
##  500-mm/750-mm plastic scintillator detector, with   ##
##  a 3-mm/6-mm Aluminium layer in between. 40-mm gap   ##
##  between the Al plates; planar geometry.             ##
##                                                      ##
##########################################################

include definitions.g4bl    #definitions

#number of simulated events
param evts=1e5

physics QGSP_BERT_EMX disable=muMinusCaptureAtRest

#Define some parameters
#param name=mudecay
param name=goldcascade

param P=0

param size=200
#param steppingVerbose=1
param worldMaterial=Vacuum maxStep=100.0
param zshift=0
param phishift=0
param dB=4
param dA1=400
param dL1=100
param histoFile=../../simdata/thesis/Glass_cav_Al2_NaI_dA1_400mm_dL1_100mm_$evts$name.root

trackcuts keep=e-,e+,gamma,mu-,mu+

# Define source
# (1) muon decay
#beam gaussian beamX=0 beamY=0 beamZ=0 meanP=$P nEvents=$evts particle=mu-

# (2) gold cascade
beam goldcascade beamX=0 beamY=0 beamZ=0 meanP=$P nEvents=$evts particle=gamma

#Geometry
tube SciBox innerRadius=0.0 outerRadius=((0.5*($dA1))+$phishift) initialPhi=0 finalPhi=360 length=$dB material=Scintillator color=$SciColor
tube SciBox2 innerRadius=10.0 outerRadius=((0.5*($dA1))+$phishift) initialPhi=0 finalPhi=360 length=$dB material=Scintillator color=$SciColor
tube AlBox innerRadius=0.0 outerRadius=(95.0+$phishift) initialPhi=0 finalPhi=360 length=2.0 material=Al2 color=$AlColor
tube AlBox2 innerRadius=10.0 outerRadius=(95.0+$phishift) initialPhi=0 finalPhi=360 length=2.0 material=Al2 color=$AlColor
tube BGOSciBox innerRadius=0.0 outerRadius=((0.5*($dA1))+$phishift) length=$dL1 material=SODIUM_IODIDE color=$SciColor
tube BGOSciBox2 innerRadius=34.0 outerRadius=((0.5*($dA1))+$phishift) length=$dL1 material=SODIUM_IODIDE color=$SciColor
tube SciBox3 innerRadius=30.0 outerRadius=34.0 initialPhi=0 finalPhi=360 length=$dL1 material=Scintillator color=$SciColor

#Cavity
tube CavTube innerRadius=(50.0+$phishift) outerRadius=(75.0+$phishift) initialPhi=0 finalPhi=360 length=20.0 material=Pyrex_Glass color=0.1,0.8,0.4,1.0
tube AlTubeU innerRadius=(75.0+$phishift) outerRadius=(95.0+$phishift) initialPhi=0 finalPhi=360 length=20.0 material=Al2 color=$AlColor
tube AlTubeS innerRadius=(45.0+$phishift) outerRadius=(95.0+$phishift) initialPhi=0 finalPhi=360 length=7.5 material=Al2 color=$AlColor

tube CavPlate innerRadius=0.0 outerRadius=(45.0+$phishift) initialPhi=0 finalPhi=360 length=2.0 material=Al2 color=$AlColor


detector SciDet1 solid=SciBox format=rootExtended material=Scintillator color=$SciColor
detector SciDet2 solid=SciBox2 format=rootExtended material=Scintillator color=$SciColor
detector SciDet3 solid=SciBox3 format=rootExtended material=Scintillator color=$SciColor
detector BGONaIDet1 solid=BGOSciBox format=rootExtended material=SODIUM_IODIDE color=$SciColor
detector BGONaIDet2 solid=BGOSciBox2 format=rootExtended material=SODIUM_IODIDE color=$SciColor


place CavTube z=0
place AlTubeU z=0
place AlTubeS z=-13.75
place AlTubeS z=13.75

place CavPlate z=11.0
place CavPlate z=-11.0

place AlBox z=34.0
place AlBox2 z=-34.0

place SciDet1 z=40.0
place SciDet2 z=-40.0
place BGONaIDet1 z=(44.0+(0.5*($dL1))+$zshift)
place BGONaIDet2 z=(-44.0-(0.5*($dL1))-$zshift)
place SciDet3 z=(-44.0-(0.5*($dL1))-$zshift)
