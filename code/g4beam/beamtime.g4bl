##########################################################
##                                                      ##
##                    beamtime.g4bl                     ##
##                                                      ##
##  Simulation of a beam source of:                     ##
##    (1) muon decay                                    ##
##    (2) gold cascade                                  ##
##  positioned at the center of two rectangular-shaped  ##
##  5-mm plastic scintillator detectors plus a 250-mm/  ##
##  500-mm/750-mm plastic scintillator detector, with   ##
##  a 3-mm/6-mm Aluminium layer in between. 40-mm gap   ##
##  between the Al plates; planar geometry.             ##
##                                                      ##
##########################################################

include definitions.g4bl    #definitions

#number of simulated events
param evts=1e5

physics QGSP_BERT_EMX disable=muMinusCaptureAtRest

#Define some parameters
#param name=mudecay
param name=goldcascade

param P=0

param size=200
#param steppingVerbose=1
param worldMaterial=Vacuum maxStep=100.0
param histoFile=../../simdata/beamtime/No_cav_black_$evts$name.root

trackcuts keep=e-,e+,gamma,mu-,mu+

# Define source
# (1) muon decay
#beam gaussian beamX=0 beamY=0 beamZ=0 meanP=$P nEvents=$evts particle=mu-

# (2) gold cascade
beam goldcascade beamX=0 beamY=0 beamZ=0 meanP=$P nEvents=$evts particle=gamma

#Geometry
tube CavTube innerRadius=50.0 outerRadius=85.0 length=20.0 material=Cu color=0.1,0.8,0.4,1.0
tube AlTubeMain innerRadius=85.0 outerRadius=105.0 length=20.0 material=Al color=$AlColor
tube AlTubeSide innerRadius=50.0 outerRadius=105.0 length=2.5 material=Al color=$AlColor
tube AlTubeFrame innerRadius=130.0 outerRadius=165.0 length=25.0 material=Al color=$AlColor
tube AlDisk innerRadius=0.0 outerRadius=50.0 length=2.0 material=Al color=$AlColor

box FrameIn width=410.0 height=410.0 length=80.1 material=Al color=$AlColor
box FrameOut width=470.0 height=470.0 length=80.0 material=Al color=$AlColor
boolean op=subtraction FrameBox FrameOut FrameIn material=Al color=$AlColor

box AlFrame width=470.0 height=470.0 length=4.0 material=Al color=$AlColor

box SciBox width=250.0 height=250.0 length=5.0 material=Scintillator color=$SciColor 
box BGOBox width=400.0 height=400.0 length=55.0 material=BGO color=$BGOColor

#Detectors
detector SciDet1 solid=SciBox format=rootExtended material=Scintillator color=$SciColor
detector SciDet2 solid=SciBox format=rootExtended material=Scintillator color=$SciColor

detector BGODet1 solid=BGOBox format=rootExtended material=BGO color=$BGOColor
detector BGODet2 solid=BGOBox format=rootExtended material=BGO color=$BGOColor

#place CavTube z=0
place AlTubeMain z=0
place AlTubeSide z=11.25
place AlTubeSide z=-11.25
place AlTubeFrame z=0
place AlDisk z=11.0
place AlDisk z=-11.0

place SciDet1 z=30.5
place SciDet2 z=-30.5

place FrameBox z=0.0
place AlFrame z=42.1
place AlFrame z=-42.1

place BGODet1 z=75.0
place BGODet2 z=-75.0



